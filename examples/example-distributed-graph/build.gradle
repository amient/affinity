group = rootProject.group
version = rootProject.version

apply plugin: 'scala'
apply plugin: "com.github.maiflai.scalatest"
apply plugin: "com.github.johnrengelman.shadow"

ext {
    mainClass = "io.amient.affinity.example.ExampleGraphMain"
    mainScript = "example-graph-api"
}

dependencies {
    compile project(':core')
    compile project(':kafka:storage-kafka')
    compile group: "ch.qos.logback", name: "logback-classic", version: "1.2.3"
    compile group: "ch.qos.logback", name: "logback-core", version: "1.2.3"
    compile 'org.scala-lang:scala-library:' + rootProject.scalaVersion
    testCompile project(':ws-client')
    testCompile project(':kafka:test-util-kafka')
    testCompile project(':spark')
    testCompile group: 'org.apache.spark', name: 'spark-core_' + rootProject.baseScalaVersion, version: project(':spark').sparkVersion
    testCompile group: 'org.apache.avro', name: 'avro-mapred', version: rootProject.avroVersion, classifier: 'hadoop2'
}


build.dependsOn shadowJar
shadowJar {
    manifest {
        attributes(
                'Main-Class': mainClass,
                'version': project.version,
        )
    }
    from { project.configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    append 'reference.conf'
}

task createStartScripts(type: CreateStartScripts) {
    outputDir = file("build/scripts")
    mainClassName = mainClass
    applicationName = mainScript
    classpath = shadowJar.outputs.files
}

build.dependsOn createStartScripts


