buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.19"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
//        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.7.1"
    }
}

group = 'io.amient.affinity'
version = '0.6.5-SNAPSHOT'

ext {
    encoding = "UTF-8"

    javaVersion = '1.8'
    junitVersion = '4.12'
    typesafeConfigVersion = '1.3.0'
    //
    scalaVersion = '2.11.8'
    scalaTestVersion = '2.2.6'
    scalaCheckVersion = '1.12.5'
    slf4jVersion = '1.7.21'
    akkaVersion = '2.4.20'
    akkaHttpVersion = '10.0.10'
    //
    avroVersion = '1.8.2' // avro version provides also org.codehouse.jackson transitively
    jacksonVersion = '2.6.5' // com.fasterxml.jackson - spark 2.x uses 2.6.5 while the newer kafka 2.9.1, as usually testing with embedded instances has conflicts
    zkClientVersion = '0.10' // zkclient provides zookeeper server transitively
    //
    baseScalaVersion = scalaVersion.split("\\.")[0] + '.' + scalaVersion.split("\\.")[1]

}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

allprojects {

    version = rootProject.version

    task compile {}

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://packages.confluent.io/maven/" }
    }

    plugins.withType(MavenPlugin).whenPluginAdded {

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        artifacts {
            archives javadocJar, sourcesJar
        }

        if (rootProject.hasProperty("maven-central")) {
            signing {
                sign configurations.archives
            }

            uploadArchives {
                repositories {

                    mavenDeployer {

                        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                            authentication(userName: ossrhUsername, password: ossrhPassword)
                        }

                        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                            authentication(userName: ossrhUsername, password: ossrhPassword)
                        }

                        if (plugins.hasPlugin(ScalaPlugin)) {
                            pom.artifactId = project.name + "_scala-" + baseScalaVersion
                        }

                        pom.project {

                            name 'Affinity Library'
                            packaging 'jar'
                            // optionally artifactId can be defined here
                            description 'Library for building fast, scalable, fault-tolerant Data APIs based on Akka, ZooKeeper and Kafka.'
                            url 'https://github.com/amient/affinity'

                            scm {
                                connection 'scm:git:https://github.com/amient/affinity.git'
                                developerConnection 'scm:git:https://github.com/amient/affinity.git'
                                url 'https://github.com/amient/affinity.git'
                            }

                            licenses {
                                license {
                                    name 'The Apache License, Version 2.0'
                                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                }
                            }

                            developers {
                                developer {
                                    id 'michal.haris'
                                    name 'Michal Haris'
                                    email 'mharis@amient.io'
                                }
                            }
                        }
                    }
                }
            }
        }
    }

    plugins.withType(JavaPlugin).whenPluginAdded {

        sourceCompatibility = rootProject.ext.javaVersion

        libsDirName = 'lib' //gradle's default is libs but CreateStartScripts needs this to be lib

        compile.dependsOn compileJava
        compile.dependsOn compileTestJava

        configurations {
            provided
            testCompile.extendsFrom provided
            all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        }

        sourceSets {
            main.compileClasspath += configurations.provided
            test {
                compileClasspath += configurations.provided
                runtimeClasspath += configurations.provided
            }
        }

        dependencies {
            compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
            testCompile group: 'junit', name: 'junit', version: junitVersion
            testCompile group: "ch.qos.logback", name: "logback-classic", version: "1.2.3"
            testCompile group: "ch.qos.logback", name: "logback-core", version: "1.2.3"
        }

    }

    plugins.withType(ScalaPlugin).whenPluginAdded {

        compile.dependsOn compileScala
        compile.dependsOn compileTestScala

        task scalaTest(dependsOn: ['testClasses'], type: JavaExec) {
            main = 'org.scalatest.tools.Runner'
            args = ['-R', "${project.buildDir}/classes/scala/integrationTest", '-o', '-h', "${project.buildDir}/test-results", '-u', "${project.buildDir}/ittest-results"]
            classpath = sourceSets.test.runtimeClasspath
            inputs.files(sourceSets.test.compileClasspath)
            outputs.dir(file("${project.buildDir}/test-results"))
        }
        test.dependsOn scalaTest

        dependencies {
            testCompile group: 'org.scalatest', name: 'scalatest_' + baseScalaVersion, version: scalaTestVersion
            testCompile group: 'org.scalacheck', name: 'scalacheck_' + baseScalaVersion, version: '1.12.5'
        }
    }

    plugins.withType(com.github.maiflai.ScalaTestPlugin).whenPluginAdded {
        dependencies {
            testRuntime 'org.pegdown:pegdown:1.6.0'
        }
    }

    tasks.withType(ScalaCompile) {
        scalaCompileOptions.additionalParameters = [
                                                   "-feature",
                                                    "-target:jvm-" + javaVersion,
                                                    "-encoding", rootProject.encoding,
                                                    "-unchecked",
                                                    "-deprecation",
                                                    "-Xfuture",
                                                    "-Yno-adapted-args",
                                                    "-Ywarn-dead-code",
                                                    "-Ywarn-numeric-widen",
                                                    "-Ywarn-unused"
                                                    ]
    }
}
